/// Evaluation structures for fluent-asserts.
/// Provides the core data types for capturing and comparing values during assertions.
module fluentasserts.core.evaluation;

import std.datetime;
import std.typecons;
import std.traits;
import std.conv;
import std.range;
import std.array;
import std.algorithm : map, sort;

import core.memory : GC;

import fluentasserts.core.memory : getNonGCMemory;
import fluentasserts.results.serializers;
import fluentasserts.results.source : SourceResult;
import fluentasserts.results.message : Message, ResultGlyphs;
import fluentasserts.results.asserts : AssertResult;
import fluentasserts.core.base : TestException;
import fluentasserts.results.printer : ResultPrinter, StringResultPrinter;
import fluentasserts.results.serializers : SerializerRegistry;

/// Holds the result of evaluating a single value.
/// Captures the value itself, any exceptions thrown, timing information,
/// and serialized representations for display and comparison.
struct ValueEvaluation {
  /// The exception thrown during evaluation
  Throwable throwable;

  /// Time needed to evaluate the value
  Duration duration;

  /// Garbage Collector memory used during evaluation (in bytes)
  size_t gcMemoryUsed;

  /// Non Garbage Collector memory used during evaluation (in bytes)
  size_t nonGCMemoryUsed;

  /// Serialized value as string
  string strValue;

  /// Proxy object holding the evaluated value to help doing better comparisions
  EquableValue proxyValue;

  /// Human readable value
  string niceValue;

  /// The name of the type before it was converted to string
  string[] typeNames;

  /// Other info about the value
  string[string] meta;

  /// The file name contining the evaluated value
  string fileName;

  /// The line number of the evaluated value
  size_t line;

  /// a custom text to be prepended to the value
  string prependText;

  /// Returns the primary type name of the evaluated value.
  /// Returns: The first type name, or "unknown" if no types are available.
  string typeName() @safe nothrow @nogc {
    if(typeNames.length == 0) {
      return "unknown";
    }

    return typeNames[0];
  }
}

/// Holds the complete state of an assertion evaluation.
/// Contains both the actual and expected values, operation metadata,
/// source location, and the assertion result.
struct Evaluation {
  /// The id of the current evaluation
  size_t id;

  /// The value that will be validated
  ValueEvaluation currentValue;

  /// The expected value that we will use to perform the comparison
  ValueEvaluation expectedValue;

  /// The operation name
  string operationName;

  /// True if the operation result needs to be negated to have a successful result
  bool isNegated;

  /// Source location data stored as struct
  SourceResult source;

  /// The throwable generated by the evaluation
  Throwable throwable;

  /// True when the evaluation is done
  bool isEvaluated;

  /// Result of the assertion stored as struct
  AssertResult result;

  /// Convenience accessors for backwards compatibility
  string sourceFile() nothrow @safe @nogc { return source.file; }
  size_t sourceLine() nothrow @safe @nogc { return source.line; }

  /// Checks if there is an assertion result with content.
  /// Returns: true if the result has expected/actual values, diff, or extra/missing items.
  bool hasResult() nothrow @safe @nogc {
    return result.hasContent();
  }

  /// Prints the assertion result using the provided printer.
  /// Params:
  ///   printer = The ResultPrinter to use for output formatting
  void printResult(ResultPrinter printer) @safe nothrow {
    if(!isEvaluated) {
      printer.primary("Evaluation not completed.");
      return;
    }

    if(!result.hasContent()) {
      printer.primary("Successful result.");
      return;
    }

    printer.info("ASSERTION FAILED: ");

    foreach(message; result.message) {
      printer.print(message);
    }

    printer.newLine;
    printer.info("OPERATION: ");

    if(isNegated) {
      printer.primary("not ");
    }

    printer.primary(operationName);
    printer.newLine;
    printer.newLine;

    printer.info("  ACTUAL: ");
    printer.primary("<");
    printer.primary(currentValue.typeName);
    printer.primary("> ");
    printer.primary(result.actual);
    printer.newLine;

    printer.info("EXPECTED: ");
    printer.primary("<");
    printer.primary(expectedValue.typeName);
    printer.primary("> ");
    printer.primary(result.expected);
    printer.newLine;

    source.print(printer);
  }

  /// Converts the evaluation to a formatted string for display.
  /// Returns: A string representation of the evaluation result.
  string toString() @safe nothrow {
    import std.string : format;

    auto printer = new StringResultPrinter();
    printResult(printer);
    return printer.toString();
  }
}

/// Evaluates a lazy input range value and captures the result.
/// Converts the range to an array and delegates to the primary evaluate function.
/// Params:
///   testData = The lazy value to evaluate
///   file = Source file (auto-captured)
///   line = Source line (auto-captured)
///   prependText = Optional text to prepend to the value display
/// Returns: A tuple containing the evaluated value and its ValueEvaluation.
auto evaluate(T)(lazy T testData, const string file = __FILE__, const size_t line = __LINE__, string prependText = null) @trusted if(isInputRange!T && !isArray!T && !isAssociativeArray!T) {
  return evaluate(testData.array, file, line, prependText);
}

/// Evaluates a lazy value and captures the result along with timing and exception info.
/// This is the primary evaluation function that serializes values and wraps them
/// for comparison operations.
/// Params:
///   testData = The lazy value to evaluate
///   file = Source file (auto-captured)
///   line = Source line (auto-captured)
///   prependText = Optional text to prepend to the value display
/// Returns: A tuple containing the evaluated value and its ValueEvaluation.
auto evaluate(T)(lazy T testData, const string file = __FILE__, const size_t line = __LINE__, string prependText = null) @trusted if(!isInputRange!T || isArray!T || isAssociativeArray!T) {
  GC.collect();
  GC.minimize();
  GC.disable();
  scope(exit) GC.enable();

  auto begin = Clock.currTime;
  alias Result = Tuple!(T, "value", ValueEvaluation, "evaluation");
  size_t gcMemoryUsed = 0;
  size_t nonGCMemoryUsed = 0;

  try {
    auto value = testData;
    alias TT = typeof(value);

    static if(isCallable!T) {
      if(value !is null) {
        begin = Clock.currTime;
        nonGCMemoryUsed = getNonGCMemory();
        // Use allocatedInCurrentThread for accurate per-thread allocation tracking
        auto gcBefore = GC.allocatedInCurrentThread();
        cast(void) value();
        gcMemoryUsed = GC.allocatedInCurrentThread() - gcBefore;
        nonGCMemoryUsed = getNonGCMemory() - nonGCMemoryUsed;
      }
    }

    auto duration = Clock.currTime - begin;
    auto serializedValue = SerializerRegistry.instance.serialize(value);
    auto niceValue = SerializerRegistry.instance.niceValue(value);

    auto valueEvaluation = ValueEvaluation(null, duration, 0, 0, serializedValue, equableValue(value, niceValue), niceValue, extractTypes!TT);
    valueEvaluation.fileName = file;
    valueEvaluation.line = line;
    valueEvaluation.prependText = prependText;
    valueEvaluation.gcMemoryUsed = gcMemoryUsed;
    valueEvaluation.nonGCMemoryUsed = nonGCMemoryUsed;


    return Result(value, valueEvaluation);
  } catch(Throwable t) {
    T result;

    static if(isCallable!T) {
      result = testData;
    }

    auto valueEvaluation = ValueEvaluation(t, Clock.currTime - begin, 0, 0, result.to!string, equableValue(result, result.to!string), result.to!string, extractTypes!T);
    valueEvaluation.fileName = file;
    valueEvaluation.line = line;
    valueEvaluation.prependText = prependText;

    return Result(result, valueEvaluation);
  }
}

version(unittest) {
  import fluentasserts.core.lifecycle;
}

@("evaluate captures an exception from a lazy value")
unittest {
  Lifecycle.instance.disableFailureHandling = false;
  int value() {
    throw new Exception("message");
  }

  auto result = evaluate(value);

  assert(result.evaluation.throwable !is null, "Expected throwable to be captured");
  assert(result.evaluation.throwable.msg == "message", "Expected msg 'message', got '" ~ result.evaluation.throwable.msg ~ "'");
}

@("evaluate captures an exception from a callable")
unittest {
  Lifecycle.instance.disableFailureHandling = false;
  void value() {
    throw new Exception("message");
  }

  auto result = evaluate(&value);

  assert(result.evaluation.throwable !is null, "Expected throwable to be captured");
  assert(result.evaluation.throwable.msg == "message", "Expected msg 'message', got '" ~ result.evaluation.throwable.msg ~ "'");
}

/// Extracts the type names for a non-array, non-associative-array type.
/// For classes, includes base classes and implemented interfaces.
/// Params:
///   T = The type to extract names from
/// Returns: An array of fully qualified type names.
string[] extractTypes(T)() if((!isArray!T && !isAssociativeArray!T) || isSomeString!T) {
  string[] types;

  types ~= unqualString!T;

  static if(is(T == class)) {
    static foreach(Type; BaseClassesTuple!T) {
      types ~= unqualString!Type;
    }
  }

  static if(is(T == interface) || is(T == class)) {
    static foreach(Type; InterfacesTuple!T) {
      types ~= unqualString!Type;
    }
  }

  return types;
}

/// Extracts the type names for an array type.
/// Appends "[]" to each element type name.
/// Params:
///   T = The array type
///   U = The element type
/// Returns: An array of type names with "[]" suffix.
string[] extractTypes(T: U[], U)() if(isArray!T && !isSomeString!T) {
  return extractTypes!(U).map!(a => a ~ "[]").array;
}

/// Extracts the type names for an associative array type.
/// Formats as "ValueType[KeyType]".
/// Params:
///   T = The associative array type
///   U = The value type
///   K = The key type
/// Returns: An array of type names in associative array format.
string[] extractTypes(T: U[K], U, K)() {
  string k = unqualString!(K);
  return extractTypes!(U).map!(a => a ~ "[" ~ k ~ "]").array;
}

@("extractTypes returns [string] for string")
unittest {
  Lifecycle.instance.disableFailureHandling = false;
  auto result = extractTypes!string;
  import std.conv : to;
  assert(result == ["string"], "Expected [\"string\"], got " ~ result.to!string);
}

@("extractTypes returns [string[]] for string[]")
unittest {
  Lifecycle.instance.disableFailureHandling = false;
  auto result = extractTypes!(string[]);
  import std.conv : to;
  assert(result == ["string[]"], "Expected [\"string[]\"], got " ~ result.to!string);
}

@("extractTypes returns [string[string]] for string[string]")
unittest {
  Lifecycle.instance.disableFailureHandling = false;
  auto result = extractTypes!(string[string]);
  import std.conv : to;
  assert(result == ["string[string]"], "Expected [\"string[string]\"], got " ~ result.to!string);
}

version(unittest) {
  interface ExtractTypesTestInterface {}
  class ExtractTypesTestClass : ExtractTypesTestInterface {}
}

@("extractTypes returns all types of a class")
unittest {
  Lifecycle.instance.disableFailureHandling = false;

  auto result = extractTypes!(ExtractTypesTestClass[]);

  assert(result[0] == "fluentasserts.core.evaluation.ExtractTypesTestClass[]", `Expected: "fluentasserts.core.evaluation.ExtractTypesTestClass[]" got "` ~ result[0] ~ `"`);
  assert(result[1] == "object.Object[]", `Expected: ` ~ result[1]);
  assert(result[2] == "fluentasserts.core.evaluation.ExtractTypesTestInterface[]", `Expected: ` ~ result[2]);
}

/// A proxy interface for comparing values of different types.
/// Wraps native values to enable equality and ordering comparisons
/// without knowing the concrete types at compile time.
interface EquableValue {
  @safe nothrow:
    /// Checks if this value equals another EquableValue.
    bool isEqualTo(EquableValue value);

    /// Checks if this value is less than another EquableValue.
    bool isLessThan(EquableValue value);

    /// Converts this value to an array of EquableValues.
    EquableValue[] toArray();

    /// Returns a string representation of this value.
    string toString();

    /// Returns a generalized version of this value for cross-type comparison.
    EquableValue generalize();

    /// Returns the serialized string representation.
    string getSerialized();
}

/// Wraps a value into an EquableValue for comparison operations.
/// Automatically selects the appropriate wrapper class based on the value type.
/// Params:
///   value = The value to wrap
///   serialized = The serialized string representation of the value
/// Returns: An EquableValue wrapping the given value.
EquableValue equableValue(T)(T value, string serialized) {
  static if(isArray!T && !isSomeString!T) {
    return new ArrayEquable!T(value, serialized);
  } else static if(isInputRange!T && !isSomeString!T) {
    return new ArrayEquable!T(value.array, serialized);
  } else static if(isAssociativeArray!T) {
    return new AssocArrayEquable!T(value, serialized);
  } else {
    return new ObjectEquable!T(value, serialized);
  }
}

/// An EquableValue wrapper for scalar and object types.
/// Provides equality and ordering comparisons for non-collection values.
class ObjectEquable(T) : EquableValue {
  private {
    T value;
    string serialized;
  }

  @trusted nothrow:
    /// Constructs an ObjectEquable wrapping the given value.
    this(T value, string serialized) {
      this.value = value;
      this.serialized = serialized;
    }

    /// Checks equality with another EquableValue.
    bool isEqualTo(EquableValue otherEquable) {
      try {
        auto other = cast(ObjectEquable) otherEquable;

        if(other !is null) {
          return value == other.value;
        }

        auto generalized = otherEquable.generalize;

        static if(is(T == class)) {
          auto otherGeneralized = cast(ObjectEquable!Object) generalized;

          if(otherGeneralized !is null) {
            return value == otherGeneralized.value;
          }
        }

        return serialized == otherEquable.getSerialized;
      } catch(Exception) {
        return false;
      }
    }

    /// Checks if this value is less than another EquableValue.
    bool isLessThan(EquableValue otherEquable) {
      static if (__traits(compiles, value < value)) {
        try {
          auto other = cast(ObjectEquable) otherEquable;

          if(other !is null) {
            return value < other.value;
          }

          return false;
        } catch(Exception) {
          return false;
        }
      } else {
        return false;
      }
    }

    /// Returns the serialized string representation.
    string getSerialized() {
      return serialized;
    }

    /// Returns a generalized version for cross-type comparison.
    EquableValue generalize() {
        static if(is(T == class)) {
          auto obj = cast(Object) value;

          if(obj !is null) {
            return new ObjectEquable!Object(obj, serialized);
          }
        }

        return new ObjectEquable!string(serialized, serialized);
    }

    /// Converts this value to an array of EquableValues.
    EquableValue[] toArray() {
      static if(__traits(hasMember, T, "byValue") && !__traits(hasMember, T, "byKeyValue")) {
        try {
          return value.byValue.map!(a => a.equableValue(SerializerRegistry.instance.serialize(a))).array;
        } catch(Exception) {}
      }

      return [ this ];
    }

    /// Returns a string representation prefixed with "Equable.".
    override string toString() {
      return "Equable." ~ serialized;
    }

    /// Comparison operator override.
    override int opCmp (Object o) {
      return -1;
    }
}

@("an object with byValue returns an array with all elements")
unittest {
  Lifecycle.instance.disableFailureHandling = false;
  class TestObject {
    auto byValue() {
      auto items = [1, 2];
      return items.inputRangeObject;
    }
  }

  auto value = equableValue(new TestObject(), "[1, 2]").toArray;
  assert(value.length == 2, "invalid length");
  assert(value[0].toString == "Equable.1", value[0].toString ~ " != Equable.1");
  assert(value[1].toString == "Equable.2", value[1].toString ~ " != Equable.2");
}

@("isLessThan returns true when value is less than other")
unittest {
  Lifecycle.instance.disableFailureHandling = false;
  auto value1 = equableValue(5, "5");
  auto value2 = equableValue(10, "10");

  assert(value1.isLessThan(value2) == true);
  assert(value2.isLessThan(value1) == false);
}

@("isLessThan returns false when values are equal")
unittest {
  Lifecycle.instance.disableFailureHandling = false;
  auto value1 = equableValue(5, "5");
  auto value2 = equableValue(5, "5");

  assert(value1.isLessThan(value2) == false);
}

@("isLessThan works with floating point numbers")
unittest {
  Lifecycle.instance.disableFailureHandling = false;
  auto value1 = equableValue(3.14, "3.14");
  auto value2 = equableValue(3.15, "3.15");

  assert(value1.isLessThan(value2) == true);
  assert(value2.isLessThan(value1) == false);
}

@("isLessThan returns false for arrays")
unittest {
  Lifecycle.instance.disableFailureHandling = false;
  auto value1 = equableValue([1, 2, 3], "[1, 2, 3]");
  auto value2 = equableValue([4, 5, 6], "[4, 5, 6]");

  assert(value1.isLessThan(value2) == false);
}

/// An EquableValue wrapper for array types.
/// Provides element-wise comparison capabilities.
class ArrayEquable(U: T[], T) : EquableValue {
  private {
    T[] values;
    string serialized;
  }

  @safe nothrow:
    /// Constructs an ArrayEquable wrapping the given array.
    this(T[] values, string serialized) {
      this.values = values;
      this.serialized = serialized;
    }

    /// Checks equality with another EquableValue by comparing serialized forms.
    bool isEqualTo(EquableValue otherEquable) {
      auto other = cast(ArrayEquable!U) otherEquable;

      if(other is null) {
        return false;
      }

      return serialized == other.serialized;
    }

    /// Arrays do not support less-than comparison, always returns false.
    bool isLessThan(EquableValue otherEquable) {
      return false;
    }

    /// Returns the serialized string representation.
    string getSerialized() {
      return serialized;
    }

    /// Converts each array element to an EquableValue.
    @trusted EquableValue[] toArray() {
      static if(is(T == void)) {
        return [];
      } else {
        try {
          auto newList = values.map!(a => equableValue(a, SerializerRegistry.instance.niceValue(a))).array;

          return cast(EquableValue[]) newList;
        } catch(Exception) {
          return [];
        }
      }
    }

    /// Arrays are already generalized, returns self.
    EquableValue generalize() {
      return this;
    }

    /// Returns the serialized string representation.
    override string toString() {
      return serialized;
    }
}

/// An EquableValue wrapper for associative array types.
/// Sorts keys for consistent comparison and inherits from ArrayEquable.
class AssocArrayEquable(U: T[V], T, V) : ArrayEquable!(string[], string) {
  /// Constructs an AssocArrayEquable, sorting entries by key.
  this(T[V] values, string serialized) {
    auto sortedKeys = values.keys.sort;

    auto sortedValues = sortedKeys
      .map!(a => SerializerRegistry.instance.niceValue(a) ~ `: ` ~ SerializerRegistry.instance.niceValue(values[a]))
      .array;

    super(sortedValues, serialized);
  }
}
